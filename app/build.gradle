apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.akumine.smartclass"
        minSdkVersion 21
        targetSdkVersion 29
        multiDexEnabled true
        versionCode 10002
        versionName "1.0.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    applicationVariants.all { variant ->
        def outputDir = new File("${project.rootDir.absolutePath}/export")
        variant.outputs.all {
            if (!variant.getBuildType().isDebuggable()) {
                variant.packageApplicationProvider.get().outputDirectory = outputDir
                outputFileName = "SmartClass-${variant.buildType.name}-v${defaultConfig.versionName}.apk"
            }
        }
        if (variant.getBuildType().isMinifyEnabled()) {
            variant.assemble.doLast {
                copy {
                    def finalFileName = "${project.name}-${variant.buildType.name}-v${defaultConfig.versionName}-mapping.txt"
                    from variant.mappingFileProvider
                    into "${outputDir}"
                    rename { String fileName -> finalFileName }
                    println "[wb_log] Mapping file will also exported to ... ${outputDir}\\${finalFileName}"
                }
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix '-DEBUG'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    configurations {
        all {
            exclude module: 'httpclient'
            exclude module: 'commons-logging'
        }
    }
}

dependencies {
    //noinspection GradleCompatible
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //android x
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    //firebase dependency
    implementation 'com.google.firebase:firebase-core:21.1.0'
    implementation 'com.google.firebase:firebase-auth:21.0.7'
    implementation 'com.google.firebase:firebase-database:20.0.5'
    implementation 'com.google.firebase:firebase-storage:20.0.1'
    implementation 'com.google.firebase:firebase-messaging:23.0.7'
    implementation 'com.google.firebase:firebase-perf:20.1.0'
    implementation 'com.google.firebase:firebase-functions:20.1.0'

    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.google.zxing:core:3.3.2'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.23.0'
    implementation('com.google.api-client:google-api-client:1.23.0') {
        exclude module: 'guava-jdk5'
    }

    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.getbase:floatingactionbutton:1.9.0'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'com.basgeekball:awesome-validation:4.2'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
